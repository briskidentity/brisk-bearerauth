plugins {
	id("java")
	id("com.diffplug.spotless")
	id("com.github.ben-manes.versions")
}

repositories {
	mavenCentral()
}

dependencyLocking {
	lockAllConfigurations()
}

java {
	sourceCompatibility = JavaVersion.VERSION_11
}

compileJava {
	options.compilerArgs += ["-Werror", "-Xlint:unchecked", "-Xlint:deprecation", "-Xlint:rawtypes", "-Xlint:varargs"]
}

test {
	maxHeapSize = "1g"
	useJUnitPlatform()
}

spotless {
	java {
		licenseHeaderFile(rootProject.file("config/spotless/license.java"))
	}
}

dependencyUpdates {
	revision = "release"
	gradleReleaseChannel = "current"
	checkConstraints = true
	rejectVersionIf {
		def isStable = { String version ->
			def isStableVersion = (version ==~ /^[0-9,.v-]+(-r)?$/)
			def isStableKeyword = ["RELEASE", "FINAL", "GA"].any({ version.toUpperCase().contains(it) })
			return isStableVersion || isStableKeyword
		}
		isStable(it.currentVersion) && !isStable(it.candidate.version)
	}
}
