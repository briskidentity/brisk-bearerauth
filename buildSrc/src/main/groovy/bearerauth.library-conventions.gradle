plugins {
    id("bearerauth.java-conventions")
    id("java-library")
    id("maven-publish")
}

group = "org.briskidentity.bearerauth"

configurations {
    internal {
        visible = false
        canBeConsumed = false
        canBeResolved = false
    }
    compileClasspath.extendsFrom(internal)
    runtimeClasspath.extendsFrom(internal)
    testCompileClasspath.extendsFrom(internal)
    testRuntimeClasspath.extendsFrom(internal)
}

dependencies {
    internal(platform(project(":brisk-bearerauth-dependencies")))
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            versionMapping {
                usage("java-api") {
                    fromResolutionResult()
                }
                usage("java-runtime") {
                    fromResolutionResult()
                }
            }
            pom {
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com:briskidentity/brisk-bearerauth.git"
                    developerConnection = "scm:git:ssh://git@github.com:briskidentity/brisk-bearerauth.git"
                    url = "https://github.com/briskidentity/brisk-bearerauth"
                }
                // TODO customize POM
            }
        }
    }
}
