import static org.briskidentity.bearerauth.build.DependencyVersionHelper.isStable

plugins {
	id("java")
	id("com.diffplug.spotless")
	id("com.github.ben-manes.versions")
}

repositories {
	mavenCentral()
}

dependencyLocking {
	lockAllConfigurations()
}

java {
	sourceCompatibility = JavaVersion.VERSION_1_8
}

compileJava {
	options.compilerArgs += ["-Werror", "-Xlint:unchecked", "-Xlint:deprecation", "-Xlint:rawtypes", "-Xlint:varargs"]
}

test {
	maxHeapSize = "1g"
	useJUnitPlatform()
}

spotless {
	java {
		licenseHeaderFile(rootProject.file("config/spotless/license.java"))
	}
}

dependencyUpdates {
	gradleReleaseChannel = "current"
	rejectVersionIf {
		isStable(it.currentVersion) && !isStable(it.candidate.version)
	}
}
